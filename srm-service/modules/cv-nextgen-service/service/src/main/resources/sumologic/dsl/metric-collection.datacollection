Var metricApiUrl = concat(Env:baseUrl, "api/v1/metrics/results")
Var body = {}
Var one = 1
Var requestIdx = 0
Var timeSeriesList = []
Var collectHostDataString = concat("", Env:collectHostData)
foreach query in Env:queries {
    Var metricIdentifier =  Env:metricIdentifiers[Var:requestIdx]
    Var metricName = Env:metricNames[Var:requestIdx]
    Var queryArr = []
    Var query_string = {}
    Var clause = concat(Var:query, " | quantize to 1m using avg")
    Var:query_string.put("query",Var:clause)
    Var:queryArr.append(Var:query_string)
    Var:body.put("query",Var:queryArr)
    Var:body.put("startTime",Env:startTimeMillis)
    Var:body.put("endTime",Env:endTimeMillis)

    Var response = sync-call(url=Var:metricApiUrl, header=Env:commonHeaders,body=Var:body)
    Var responseRows = extract(Var:response,$.response)
    Var metricDataResultRow = Var:responseRows[0]
    Var results = extract(Var:metricDataResultRow,$.results)
    Var lengthOfResults = Var:results.length()
    Var resultIndex = 0

     while Var:resultIndex !=  Var:lengthOfResults{
         Var metricDataResult = Var:results[Var:resultIndex]
         Var timeseries = extract(Var:metricDataResult,$.datapoints.timestamp)
         Var values = extract(Var:metricDataResult,$.datapoints.value)
         Var dimensionObj = extract(Var:metricDataResult,$.metric.dimensions)
         Var lengthOfDimensions = Var:dimensionObj.length()
         Var dimensionIdx = 0
         Var hostName = ""
         if Var:collectHostDataString == "true" {
             while Var:dimensionIdx != Var:lengthOfDimensions{
                 Var dimension = Var:dimensionObj[Var:dimensionIdx]
                 Var key = extract(Var:dimension,$.key)
                 if Var:key == Env:serviceInstanceIdentifierTag {
                     hostName = extract(Var:dimension,$.value)
                 }
                 dimensionIdx = Var:dimensionIdx + Var:one
             }
         }
         Var lengthOfItems = Var:values.length()
         Var index = 0
         while Var:index !=  Var:lengthOfItems{
             Var value = Var:values[Var:index]
             Var timestamp = Var:timeseries[Var:index]
             if Var:collectHostDataString != "true" {
                 Var tsRecord = new TimeseriesRecord(txnName=Env:groupName, metricIdentifier=Var:metricIdentifier, metricName=Var:metricName, metricValue=Var:value, timestamp=Var:timestamp)
                 Var:timeSeriesList.append(Var:tsRecord)
             }
             if Var:collectHostDataString == "true" {
                 Var tsRecord = new TimeseriesRecord(txnName=Env:groupName, metricIdentifier=Var:metricIdentifier, metricName=Var:metricName, metricValue=Var:value, timestamp=Var:timestamp, host=Var:hostName)
                 Var:timeSeriesList.append(Var:tsRecord)
             }
             index = Var:index + Var:one
         }
         resultIndex = Var:resultIndex + Var:one
     }
     requestIdx = Var:requestIdx + Var:one
 }

return Var:timeSeriesList