Var dataQuery = "api/v1/query_range?"
Var lengthOfItems = Env:queryList.length()
Var index = 0
Var one = 1
Var returnList = []
Var hostQueryAddon = ""
Var maximumHostSizeAllowed = 100

while Var:index !=  Var:lengthOfItems {
    Var metricName = Env:metricNameList[Var:index]
    Var query = Env:queryList[Var:index]
    Var filters = Env:filterList[Var:index]
    Var serviceInstanceField = Env:serviceInstanceFieldList[Var:index]
    Var metricIdentifier =  Env:metricIdentifiers[Var:index]

    if Env:collectHostData == "true" {
        Var hostQuery = concat(Env:baseUrl, "api/v1/label/", Var:serviceInstanceField, "/values?start=", Env:startTimeSeconds,"&end=", Env:endTimeSeconds, "&match[]={", Var:filters, "}")
        Var hostResult = sync-call(url=Var:hostQuery, header=Env:commonHeaders)
        Var hostList = extract(Var:hostResult,$.data)
        Var hostListLength = Var:hostList.length()

        if Var:maximumHostSizeAllowed < Var:hostListLength {
            Var exceptionMessage = concat("Host list returned from Prometheus is of size ", Var:hostListLength, ", which is greater than allowed ",  Var:maximumHostSizeAllowed, ", please check the query")
            throw exception Var:exceptionMessage
        }
        foreach host in Var:hostList {
            Var hostFilter = concat(",",Var:serviceInstanceField, "=\"", Var:host, "\"}")
            print Var:hostFilter
            Var:query.replaceAll("}", Var:hostFilter)
            Var fullQuery = concat(Env:baseUrl, Var:dataQuery, "start=", Env:startTimeSeconds, "&end=", Env:endTimeSeconds, "&step=60s&query=", Var:query)
            print Var:fullQuery
            Var result = sync-call(url=Var:fullQuery, header=Env:commonHeaders)
            Var resultList = extract(Var:result, $.data.result)
            foreach metric in Var:resultList {
              Var data = extract(Var:metric,$.values)
              foreach meticData in Var:data {
                Var val = extract(Var:meticData,$.[1])
                Var timeStampVal = extract(Var:meticData,$.[0])
                Var tsRecord = new TimeseriesRecord(txnName=Env:groupName, metricIdentifier=Var:metricIdentifier, metricName=Var:metricName, metricValue=Var:val, timestamp=Var:timeStampVal, host=Var:host)
                Var:returnList.append(Var:tsRecord)
              }
            }
            Var:query = Env:queryList[Var:index]
        }

    }


    if Env:collectHostData != "true" {
        Var fullQuery = concat(Env:baseUrl, Var:dataQuery, "start=", Env:startTimeSeconds, "&end=", Env:endTimeSeconds, "&step=60s&query=", Var:query)
        print Var:fullQuery
        Var result = sync-call(url=Var:fullQuery, header=Env:commonHeaders)
        Var resultList = extract(Var:result, $.data.result)
        foreach metric in Var:resultList {
          Var data = extract(Var:metric,$.values)
          foreach meticData in Var:data {
            Var val = extract(Var:meticData,$.[1])
            Var timeStampVal = extract(Var:meticData,$.[0])
            Var tsRecord = new TimeseriesRecord(txnName=Env:groupName, metricIdentifier=Var:metricIdentifier, metricName=Var:metricName, metricValue=Var:val, timestamp=Var:timeStampVal)
            Var:returnList.append(Var:tsRecord)
          }
        }
    }


    index = Var:index + Var:one
}

return Var:returnList